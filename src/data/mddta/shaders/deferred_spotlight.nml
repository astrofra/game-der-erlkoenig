<NML = 1.0>

<Shader =
	<Name = "core::projective_spotlight">

	<Vertex = "
		varying	vec3		forward;
		void	main()
		{
			gl_Position = gl_ModelViewProjectionMatrix * gl_Vertex;
			forward = (gl_ModelViewMatrix * gl_Vertex).xyz;
		}
	">

	<Pixel = "
		varying	vec3		forward;
		uniform	vec2		inverse_buffer_size;
		uniform	float		view_depth_offset;

		uniform mat4		light_projection_matrix;
		uniform vec3		diffuse_color,
							specular_color,
							position_view_space,
							direction_view_space;

		uniform	float		range,
							spot_edge_angle,
							spot_cone_angle;

		uniform	sampler2D	gbuffer0, gbuffer1, gbuffer2;
		uniform	sampler2D	projection_texture;

		void	main()
		{
			vec2	UV = vec2(gl_FragCoord.x, gl_FragCoord.y) * inverse_buffer_size;
			vec4	norm_dpth = texture2D(gbuffer0, UV);
			vec3	frag_viewpos = (forward / forward.z) * (norm_dpth.w + view_depth_offset);

			// Normal in viewmodel space.
			vec3	normal = norm_dpth.xyz;

			// Light diffuse contribution.
			vec3	dt = frag_viewpos - position_view_space;
			float	dl = length(dt);
			dt = dt / dl;
			float	atten = range > 0.0 ? max(1.0 - dl / range, 0.0) : 1.0;
			float	idiff = sqrt(max(-dot(dt, normal), 0.0)) * atten;

			// Projective map.
			vec4	pj_coord = light_projection_matrix * vec4(frag_viewpos, 1.0);
			if	(pj_coord.w < 0.5)
				discard;
			vec2	pj_biased = ((pj_coord.xy / pj_coord.w) + 1.0) * 0.5;

			// Clamp when outside of cone.
			float	sdiff = dot(direction_view_space, dt) < (spot_cone_angle - spot_edge_angle) ? 0.0 : 1.0;

			// Full contribution.
			vec4	diff_bake = texture2D(gbuffer1, UV);
			gl_FragColor = vec4(texture2D(projection_texture, pj_biased).rgb * diff_bake.rgb * diffuse_color * diff_bake.w * idiff * sdiff, 1.0);
		}
	">
>

<Shader =
	<Name = "core::projective_spotlight_shadow">

	<Vertex = "
		varying	vec3		forward;
		void	main()
		{
			gl_Position = gl_ModelViewProjectionMatrix * gl_Vertex;
			forward = (gl_ModelViewMatrix * gl_Vertex).xyz;
		}
	">

	<Pixel = "
		varying	vec3		forward;

		uniform	vec2		inverse_buffer_size;
		uniform	float		view_depth_offset;

		uniform mat4		light_projection_matrix;
		uniform vec3		diffuse_color,
							specular_color,
							position_view_space;
		uniform	float		range,
							spot_edge_angle,
							spot_cone_angle;

		uniform	sampler2D	gbuffer0, gbuffer1, gbuffer2;
		uniform	sampler2D	projection_texture;

		void	main()
		{
			vec2	UV = vec2(gl_FragCoord.x, gl_FragCoord.y) * inverse_buffer_size;
			vec4	norm_dpth = texture2D(gbuffer0, UV);
			vec3	frag_viewpos = (forward / forward.z) * (norm_dpth.w + view_depth_offset);

			// Normal in viewmodel space.
			vec3	normal = norm_dpth.xyz;

			// Light diffuse contribution.
			vec3	dt = frag_viewpos - position_view_space;
			float	dl = length(dt);
			dt = dt / dl;
			float	atten = range > 0.0 ? max(1.0 - dl / range, 0.0) : 1.0;
			float	ishadow = pow(max(-dot(dt, normal), 0.0) * atten, 0.25);

			// Projective map.
			vec4	pj_coord = light_projection_matrix * vec4(frag_viewpos, 1.0);
			vec3	pj_biased = ((pj_coord.xyz / pj_coord.q) + 1.0) * 0.5;

			// Full contribution.
			gl_FragColor = texture2D(projection_texture, pj_biased.xy).rgba * ishadow;
		}
	">
>

<Shader =
	<Name = "core::spotlight">

	<Vertex = "
		varying	vec3		forward;
		void	main()
		{
			gl_Position = gl_ModelViewProjectionMatrix * gl_Vertex;
			forward = (gl_ModelViewMatrix * gl_Vertex).xyz;
		}
	">

	<Pixel = "
		varying	vec3		forward;

		uniform	vec2		inverse_buffer_size;
		uniform	float		view_depth_offset;

		uniform vec3		diffuse_color,
							specular_color,
							position_view_space,
							direction_view_space;

		uniform	float		range,
							spot_edge_angle,
							spot_cone_angle;

		uniform	sampler2D	gbuffer0, gbuffer1, gbuffer2;

		void	main()
		{
			vec2	UV = vec2(gl_FragCoord.x, gl_FragCoord.y) * inverse_buffer_size;
			vec4	norm_dpth = texture2D(gbuffer0, UV);
			vec3	frag_viewpos = (forward / forward.z) * (norm_dpth.w + view_depth_offset);

			// Normal in viewmodel space.
			vec3	normal = norm_dpth.xyz;

			// Light diffuse contribution.
			vec3	dt = frag_viewpos - position_view_space;
			float	dl = length(dt);
			dt = dt / dl;
			float	atten = range > 0.0 ? max(1.0 - dl / range, 0.0) : 1.0;
			float	idiff = max(-dot(dt, normal) * atten, 0.0);

			float	sdiff = dot(direction_view_space, dt);
			if	(sdiff < spot_cone_angle)
			{
				if	(sdiff < 0.0)
					sdiff = 0.0;
				else
					sdiff = max((sdiff - spot_edge_angle) / (spot_cone_angle - spot_edge_angle), 0.0);
			}
			else	sdiff = 1.0;

			// Full contribution.
			vec4	diff_bake = texture2D(gbuffer1, UV);
			gl_FragColor = vec4(diff_bake.rgb * diff_bake.w * diffuse_color * idiff * sdiff, 1.0);
		}
	">
>

<Shader =
	<Name = "core::spotlight_shadowmap">

	<Vertex = "
		varying	vec3		forward;

		void	main()
		{
			gl_Position = gl_ModelViewProjectionMatrix * gl_Vertex;
			forward = (gl_ModelViewMatrix * gl_Vertex).xyz;
		}
	">

	<Pixel = "
		varying	vec3		forward;

		uniform	vec2		inverse_buffer_size;
		uniform	float		view_depth_offset,
							shadow_bias;

		uniform vec3		diffuse_color,
							specular_color,
							position_view_space,
							direction_view_space;

		uniform	float		range,
							spot_edge_angle,
							spot_cone_angle,
							inverse_shadowmap_size;

		uniform	sampler2DShadow 	light_view_depth_texture;
		uniform	sampler2D	noise_texture;

		uniform	sampler2D	gbuffer0, gbuffer1, gbuffer2;

		uniform	mat4		view_to_light_matrix;
		uniform	mat4		light_projection_matrix;

		//---------------------------
		float	EvaluatePCF(vec3 pjb)
		//---------------------------
		{
			// 3x3 PCF TAP.
			float	pcf_tap = 0.0;
			for (float v = 0.0; v < 3.0; ++v)
				for (float u = 0.0; u < 3.0; ++u)
					pcf_tap += shadow2D(light_view_depth_texture, pjb + vec3(u, v, 0.0) * inverse_shadowmap_size).r;

			return pcf_tap / 9.0;
//			return shadow2D(light_view_depth_texture, pj_biased).r;
		}

		//------------
		void	main()
		{
			vec2	UV = vec2(gl_FragCoord.x, gl_FragCoord.y) * inverse_buffer_size;
			vec4	norm_dpth = texture2D(gbuffer0, UV);
			vec3	frag_viewpos = (forward / forward.z) * (norm_dpth.w + view_depth_offset - shadow_bias);

			vec4	frag_lightpos = light_projection_matrix * vec4(frag_viewpos, 1.0);
			vec3	pj_biased = ((frag_lightpos.xyz / frag_lightpos.w) + 1.0) * 0.5;

			float	pcf = EvaluatePCF(pj_biased);

			if	(pcf > 0.0)
			{
				// Normal in viewmodel space.
				vec3	normal = norm_dpth.xyz;

				// Light diffuse contribution.
				vec3	dt = frag_viewpos - position_view_space;
				float	dl = length(dt);
				dt = dt / dl;
				float	atten = range > 0.0 ? max(1.0 - dl / range, 0.0) : 1.0;
				float	idiff = max(-dot(dt, normal) * atten, 0.0);

				float	sdiff = dot(direction_view_space, dt);
				if	(sdiff < spot_cone_angle)
				{
					if	(sdiff < 0.0)
						sdiff = 0.0;
					else
						sdiff = max((sdiff - spot_edge_angle) / (spot_cone_angle - spot_edge_angle), 0.0);
				}
				else	sdiff = 1.0;

				// Light specular contribution.
				vec4	spec_glos = texture2D(gbuffer2, UV);
				vec3	e = reflect(normalize(frag_viewpos), normal);

				float	spec_atten = min((atten + 9.0) / 10.0, 1.0);
				float	ispec = pow(max(-dot(dt, normalize(e)) * spec_atten * sdiff, 0.0), spec_glos.w * 96.0);

				// Full contribution.
				vec4	diff_bake = texture2D(gbuffer1, UV);
				gl_FragColor = vec4(diff_bake.w * (diff_bake.rgb * diffuse_color * idiff * sdiff + spec_glos.rgb * specular_color * ispec), 1.0) * pcf;
			}
			else	gl_FragColor = vec4(0.0, 0.0, 0.0, 0.0);
		}
	">
>


<Shader =
	<Name = "core::projective_spotlight_shadowmap">

	<Vertex = "
		varying	vec3		forward;
		void	main()
		{
			gl_Position = gl_ModelViewProjectionMatrix * gl_Vertex;
			forward = (gl_ModelViewMatrix * gl_Vertex).xyz;
		}
	">
	<Pixel = "
		varying	vec3		forward;

		uniform	vec2		inverse_buffer_size;
		uniform	float		view_depth_offset,
							shadow_bias;

		uniform vec3		diffuse_color,
							specular_color,
							position_view_space,
							direction_view_space;

		uniform	float		range,
							spot_edge_angle,
							spot_cone_angle,
							inverse_shadowmap_size;

		uniform	sampler2DShadow 	light_view_depth_texture;
		uniform	sampler2D	noise_texture,
							projection_texture;

		uniform	sampler2D	gbuffer0, gbuffer1, gbuffer2;

		uniform	mat4		view_to_light_matrix,
							light_projection_matrix;

		//---------------------------------
		float	EvaluatePCF(vec3 pj_biased)
		{
/*
			return	(
						shadow2D(light_view_depth_texture, pj_biased).r +
						shadow2D(light_view_depth_texture, pj_biased + vec3(1.0, 0.0, 0.0) * inverse_shadowmap_size).r +
						shadow2D(light_view_depth_texture, pj_biased + vec3(0.0, 1.0, 0.0) * inverse_shadowmap_size).r +
						shadow2D(light_view_depth_texture, pj_biased + vec3(1.0, 1.0, 0.0) * inverse_shadowmap_size).r
					) * 0.25;
*/
			return shadow2D(light_view_depth_texture, pj_biased).r;
		}

		//------------
		void	main()
		{
			vec2	UV = vec2(gl_FragCoord.x, gl_FragCoord.y) * inverse_buffer_size;
			vec4	norm_dpth = texture2D(gbuffer0, UV);

			// Unpack depth and compute fragment position in view space.
			vec3	frag_viewpos = (forward / forward.z) * (norm_dpth.w + view_depth_offset - shadow_bias);

			vec4	frag_lightpos = light_projection_matrix * vec4(frag_viewpos, 1.0);
			vec3	pj_biased = ((frag_lightpos.xyz / frag_lightpos.w) + 1.0) * 0.5;

			float	pcf = EvaluatePCF(pj_biased);

			if	(pcf > 0.0)
			{
				// Normal in viewmodel space.
				vec3	normal = norm_dpth.xyz;

				// Light diffuse contribution.
				vec3	dt = frag_viewpos - position_view_space;
				float	dl = length(dt);
				dt = dt / dl;
				float	atten = range > 0.0 ? max(1.0 - dl / range, 0.0) : 1.0;
				float	idiff = sqrt(max(-dot(dt, normal), 0.0)) * atten;

				// Clamp when outside of cone.
				float	sdiff = dot(direction_view_space, dt) < (spot_cone_angle - spot_edge_angle) ? 0.0 : 1.0;

				// Full contribution.
				vec4	diff_bake = texture2D(gbuffer1, UV);
				gl_FragColor = vec4(texture2D(projection_texture, pj_biased.xy).rgb * diff_bake.rgb * diffuse_color * diff_bake.w * idiff * sdiff, 1.0) * pcf;
			}
			else	gl_FragColor = vec4(0.0, 0.0, 0.0, 0.0);
		}
	">
>

<Shader =
	<Name = "core::spotlight_specular">

	<Vertex = "
		varying	vec3		forward;
		void	main()
		{
			gl_Position = gl_ModelViewProjectionMatrix * gl_Vertex;
			forward = (gl_ModelViewMatrix * gl_Vertex).xyz;
		}
	">
	<Pixel = "
		varying	vec3		forward;

		uniform	vec2		inverse_buffer_size;
		uniform	float		view_depth_offset;

		uniform vec3		diffuse_color,
							specular_color,
							position_view_space,
							direction_view_space;

		uniform	float		range,
							spot_edge_angle,
							spot_cone_angle;

		uniform	sampler2D	gbuffer0, gbuffer1, gbuffer2;

		void	main()
		{
			vec2	UV = vec2(gl_FragCoord.x, gl_FragCoord.y) * inverse_buffer_size;
			vec4	norm_dpth = texture2D(gbuffer0, UV);

			// Fragment to view space.
 			vec3	fragment_position = (forward / forward.z) * (norm_dpth.w + view_depth_offset);

			// Normal in viewmodel space.
			vec3	normal = norm_dpth.xyz;

			// Light diffuse contribution.
			vec3	dt = fragment_position - position_view_space;
			float	dl = length(dt);
			dt = dt / dl;
			float	atten = range > 0.0 ? max(1.0 - dl / range, 0.0) : 1.0;
			float	idiff = max(-dot(dt, normal) * atten, 0.0);

			float	sdiff = dot(direction_view_space, dt);
			if	(sdiff < spot_cone_angle)
					sdiff = sdiff < 0.0 ? 0.0 : max((sdiff - spot_edge_angle) / (spot_cone_angle - spot_edge_angle), 0.0);
			else	sdiff = 1.0;

			// Light specular contribution.
			vec4	spec_glos = texture2D(gbuffer2, UV);
			vec3	e = reflect(normalize(fragment_position), normal);
			float	spec_atten = min((atten + 9.0) / 10.0, 1.0);
			float	ispec = pow(max(-dot(dt, normalize(e)) * spec_atten * sdiff, 0.0), spec_glos.w * 96.0);

			// Full contribution.
			vec4	diff_bake = texture2D(gbuffer1, UV);
			gl_FragColor = vec4(diff_bake.w * (diff_bake.rgb * diffuse_color * idiff * sdiff + spec_glos.rgb * specular_color * ispec), 1.0);
		}
	">
>
